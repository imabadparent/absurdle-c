_pkgname=absurdle-c
pkgname=absurdle-c-git
pkgver=r26.600b72b
pkgrel=1
pkgdesc='A clone of absurdle in C'
arch=('x86_64')
license=('GPL')
url="https://github.com/imabadparent/${_pkgname}"
provides=("${_pkgname}")
conflicts=("${_pkgname}")
makedepends=('gcc' 'cmake' 'git')
depends=('ncurses')
source=("${_pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "${srcdir}/${_pkgname}" \
          -DCMAKE_BUILD_TYPE='Release' \
          -DCMAKE_INSTALL_PREFIX='/usr' \
          -Wno-dev
    cmake --build build
}

package() {
    DESTDIR="${pkgdir}" cmake --install build
    cd "${srcdir}/${_pkgname}"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${_pkgname}/README.md"
}
